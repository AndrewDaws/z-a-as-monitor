#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2019 Sebastian Gniazdowski

# Set the base and typically useful options
emulate -LR zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops rcquotes

[[ ${ZPLG_ICE[as]} != track* && ${ZPLG_ICE[.track]} != track* ]] && return 0
[[ $1 = plugin ]] && {
    print -P -- "%F{38}as-track annex: %F{160}ERROR: as'${ZPLG_ICE[as]}' ice can be used only with snippets%f"
    return 0
}

local __type="$1" __url="$2" __id_as="$3" __dir="$4" __hook="$5"

ZPLG_ICE[.track]=${ZPLG_ICE[as]}

if [[ ${ZPLG_ICE[as]} = track\|(command|program) ]] {
    ZPLG_ICE[as]=command
} elif [[ ${ZPLG_ICE[as]} = track\|completion ]] {
    ZPLG_ICE[as]=completion
} elif [[ ${ZPLG_ICE[as]} = track\|null ]] {
    ZPLG_ICE[as]=null
} else {
    unset 'ZPLG_ICE[as]'
}

if [[ -d $__dir && -z ${opts[(r)-u]} ]] {
    return 0
}

(( ${+functions[-zplg-setup-plugin-dir]} )) || builtin source ${ZPLGM[BIN_DIR]}/zplugin-install.zsh

if [[ -z ${ZPLG_ICE[dlpage]} ]] {
    local plus=${(MS)__url%%[^+]++##} dlpage=${__url}
    plus=${plus#?+}
    while [[ -n $plus ]] {
        dlpage=${dlpage:h}
        plus=${plus%+}
    }
    local dlpage=${dlpage}/${__url##*++}
} else {
    local dlpage=${ZPLG_ICE[dlpage]}
}

local tmpfile="$(mktemp)"

-zplg-download-file-stdout $dlpage 1 >! $tmpfile || {
    -zplg-download-file-stdout $dlpage >! $tmpfile || {
        print -P "%F{38}as-track annex: %F{160}ERROR: couldn't" \
            "fetch the download page (%F{221}${dlpage//\%/%%}%F{160})%f"
        return 0
    }
}

match=()
local pattern_url=${__url%(#b)([^+])++*}
pattern_url=$pattern_url${match[1]}

pattern_url=${pattern_url//\%VERSION\%/[.,a-zA-Z0-9_-]\\+}

local -a list
list=( ${(@f)"$(command grep -io "href=.$pattern_url" $tmpfile)"} )

local selected=${list[1]#href=?}

if [[ -z $selected ]] {
    print -P -- "%F{38}as-track annex: %F{160}Warning: couldn't match the URL at the download page%f"
    print -P -- "%F{160}(which is %F{221}${__url//\%/%%}%F{160}"\
"${${${__id_as:#$__url}:+\\nthe snippet is being identified as %F\{141\}$__id_as%F\{160\}\)}:-\)}"
    return 9
}

if [[ $selected = /* ]] {
    local domain
    if [[ $dlpage = (#b)(#i)((http(s|)|ftp(s|)|ssh|scp)://[^/]##)(*) ]] {
        domain=${match[1]}
    } else {
        if [[ -z ${match[2]} ]] {
            print -P -- "%F{38}as-track annex: %F{160}ERROR: the URL cannot be only a domain name%f"
            return 9
        } else {
            print -P -- "%F{38}as-track annex: %F{160}ERROR: couldn't establish the domain name" \
                "(unsupported protocol? supported are: http(s),ftp(s),ssh,scp)%f"
            return 9
        }
    }
    local new_url=$domain$selected
} elif [[ $selected = (#i)(http(s|)|ftp(s|)|ssh|scp)://* ]] {
    local new_url=$selected
} else {
    local new_url=${dlpage:h}/$selected
}

# Assign outer-scope parameters
url=$new_url

return 0

# vim:ft=zsh:tw=80:sw=4:sts=4:et
